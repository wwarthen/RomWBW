10 PRINT "REVERSE"
30 PRINT "CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
100 PRINT "REVERSE -- A GAME OF SKILL"
140 REM *** N=NUMBER OF NUMBERS
150 N=9
160 INPUT "DO YOU WANT THE RULES (0=NO,1=YES)"A
180 IF A=0 GOTO 210
190 GOSUB 710
200 REM *** MAKE A RANDOM LIST @(1) TO @(N)
210 @(1)=RND(N-1)+1
220 FOR K=2 TO N
230 @(K)=RND(N)
240 FOR J=1 TO K-1
250 IF @(K)=@(J) GOTO 230
260 NEXT J
270 NEXT K
280 REM *** PRINT ORIGINAL LIST AND START GAME
290 PRINT
300 PRINT "HERE WE GO ... THE LIST IS:"
310 T=0
320 GOSUB 610
330 INPUT "HOW MANY SHALL I REVERSE"R
350 IF R=0 GOTO 520
360 IF R<=N GOTO 390
370 PRINT "OOPS! TOO MANY! I CAN REVERSE AT MOST ",#2,N
380 GOTO 330
390 T=T+1
400 REM *** REVERSE R NUMBERS AND PRINT NEW LIST
410 FOR K=1 TO R/2
420 Z=@(K)
430 @(K)=@(R-K+1)
440 @(R-K+1)=Z
450 NEXT K
460 GOSUB 610
470 REM *** CHECK FOR A WIN
480 FOR K=1 TO N
490 IF @(K)#K GOTO 330
500 NEXT K
510 PRINT "YOU WON IT IN",T," MOVES!!!"
520 PRINT
530 INPUT "TRY AGAIN (1=YES, 0=NO)"A
550 IF A=1 GOTO 210
560 PRINT
565 PRINT "O.K. HOPE YOU HAD FUN!!"
570 GOTO 999
600 REM *** SUBROUTINE TO PRINT LIST
610 PRINT
620 FOR K=1 TO N
630 PRINT #2,@(K),
640 NEXT K
650 PRINT
660 RETURN
700 REM *** SUBROUTINE TO PRINT THE RULES
710 PRINT "THIS IS THE GAME OF 'REVERSE'. TO WIN, ALL YOU HAVE"
720 PRINT "TO DO IS ARRANGE A LIST OF NUMBERS (1 THROUGH ",#2,N,")"
730 PRINT "IN NUMERICAL ORDER FROM LEFT TO RIGHT.  TO MOVE, YOU"
740 PRINT "TELL ME HOW MANY NUMBERS (COUNTING FROM THE LEFT) TO"
750 PRINT "REVERSE.  FOR EXAMPLE, IF THE CURRENT LIST IS:"
755 PRINT
760 PRINT " 2 3 4 5 1 6 7 8 9"
765 PRINT
770 PRINT "AND YOU REVERSE 4, THE RESULT WILL BE:"
775 PRINT
780 PRINT " 5 4 3 2 1 6 7 8 9"
785 PRINT
790 PRINT "NOW IF YOU REVERSE 5, YOU WIN!"
795 PRINT
800 PRINT " 1 2 3 4 5 6 7 8 9"
805 PRINT
810 PRINT "NO DOUBT YOU WILL LIKE THIS GAME, BUT"
820 PRINT "IF YOU WANT TO QUIT, REVERSE 0 (ZERO)."
830 PRINT
840 RETURN
999 END
